# EIP-712: 强类型的链上数据签名

当我们在区块链项目中使用以太坊智能合约时，身份验证和数据完整性是两个非常重要的考量因素。

EIP-712 正是为了解决这些问题而生。它定义了一种以太坊消息的标准结构，使得这些消息可以在用户界面中更可读，并支持在链上验证这些消息的来源和完整性。

## 什么是EIP-712？

EIP-712 是一个以太坊改进提案，引入了一种新的方法来生成加密签名。

在 EIP-712 前，用户通常需要对一个简单的字符串或整个交易数据进行签名，这往往难以读懂且易于被滥用。EIP-712 通过使用结构化数据提出了更安全、更清晰的签名方案。

## 核心特性

核心特性包括：

- 类型安全：通过定义数据的结构，使得数据处理更加安全。
- 用户友好：签名数据被展示为结构化的形式，易于理解。
- 高效的合约内验证：允许智能合约内部直接且高效地验证签名。

##结构化数据签名流程

EIP-712 提供了一个标准的方法来签名结构化数据。这包括几个关键步骤，让我们逐一了解：

1. 创建域分隔符 (EIP712Domain Separator)

    域分隔符定义了签名消息的上下文环境，例如合约的名称、版本号、链 ID 和验证合约地址等。这可以帮助确保签名在正确的环境中被验证。

    域分隔符**可以**包含以下字段，具体取决于协议设计者的需求：
    - string name：用户可读的签名域名称，通常是 DApp 或协议的名称。
    - string version：签名域的当前版本号，不同版本的签名可能不兼容。
    - uint256 chainId：EIP-155 的链 ID，用户代理应当对不匹配当前活动链的签名请求拒绝。
    - address verifyingContract：将用来验证签名的智能合约地址。
    - bytes32 salt：用于消除模糊性的盐值。

    示例代码：
    ```
    bytes32 domainSeparator = keccak256(
        abi.encode(
            keccak256("EIP712Domain(string name,string version,uint256 chainId,address verifyingContract)"),
            keccak256(bytes(name)),
            keccak256(bytes(version)),
            chainId,
            verifyingContract
        )
    );
    ```

2. 创建结构化数据的哈希

    假设我们有一个名为 Order 的结构，它包括以下字段：
    ```
    struct Order {
        address from;
        address to;
        uint256 amount;
    }
    ```

    首先定义并创建该结构的类型哈希：

    `string constant TYPEHASH = keccak256("Order(address from,address to,uint256 amount)");`

    然后将类型哈希与实际数据值一起编码并哈希：
    ```
    bytes32 structHash = keccak256(
        abi.encode(
            TYPEHASH,
            order.from,
            order.to,
            order.amount
        )
    );
    ```

3. 生成最终签名哈希

    结合域分隔符和结构化数据哈希，创建最终的签名哈希：
    ```
    bytes32 digest = keccak256(
        abi.encodePacked(
            "\x19\x01",
            domainSeparator,
            structHash
        )
    );
    ```

4. 在客户端进行签名

    使用客户端（例如，使用 ethers.js 和 MetaMask）让用户使用私钥对最终的哈希进行签名。下面是 JavaScript 示例代码：
    ```
    const domain = {
        name: 'Example DApp',
        version: '1',
        chainId: 1, // 主网络的 chainId 为1, 测试网络则有不同的值
        verifyingContract: '合约地址'
    };

    // 所有类型的定义
    const types = {
        EIP712Domain: [
            { name: 'name', type: 'string' },
            { name: 'version', type: 'string' },
            { name: 'chainId', type: 'uint256' },
            { name: 'verifyingContract', type: 'address' },
        ],
        Order: [
            { name: 'from', type: 'address' },
            { name: 'to', type: 'address' },
            { name: 'amount', type: 'uint256' }
        ]
    };


    // 创建一个符合 Order 结构的消息实例。
    const order = {
        from: '0xfrom...',
        to: '0xto...',
        amount: 1000
    };

    async function requestSignature(order) {
        // 使用 ethers 连接到以太坊钱包
        const provider = new ethers.providers.Web3Provider(window.ethereum);

        await provider.send('eth_requestAccounts', []); // 请求用户授权
        const signer = provider.getSigner();

        const signature = await signer._signTypedData(domain, types, order);
        console.log('Signature:', signature);
    }
    ```

5. 验证签名：在智能合约中确认签名的有效性和数据的一致性

    使用 `ecrecover` 函数来验证签名，确保签名者的地址与预期相匹配，从而验证签名的真实性和完整性。

    签名验证步骤如下：
    
        - 构造消息哈希：为了验证签名，首先需要重建签名时所用的相同消息哈希。这通常涉及将原始参数按照指定格式进行序列化与哈希处理。
        - 使用 `ecrecover` 函数恢复签名者的地址：Solidity 语言提供的 `ecrecover` 函数能够通过给定的消息哈希值及签名（包括r, s, v三个参数）来确定签名者的地址。
        - 校验恢复的地址与预期地址是否匹配：通过比较 `ecrecover` 函数输出的地址与事先定义的期望地址，确保恢复出的地址与预期一致，从而验证签名者的身份。这一步是确认交易发起者身份合法性的关键环节。

## 完整合约示例

让我们通过一个具体的例子来看看 EIP-712 是如何应用的。假设我们需要让用户签名一份包含订单信息的合同：

```
pragma solidity ^0.8.0;

import "@openzeppelin/contracts/utils/cryptography/draft-EIP712.sol";

contract OrderContract is EIP712 {
    // Struct for EIP-712, which represents some type of order
    struct Order {
        address from;
        address to;
        uint256 amount;
    }

    // Domain separator will be automatically set
    constructor() EIP712("Order", "1") {}

    // 对 order 数据进行哈希处理
    function hashOrder(Order memory order) private view returns (bytes32) {
        return _hashTypedDataV4(
            keccak256(
                abi.encode(keccak256("Order(address from,address to,uint256 amount)"),
                order.from,
                order.to,
                order.amount
            )
        ));
    }

    // 验证 order 签名
    function verifyOrder(Order memory order, bytes memory signature) public view returns (bool) {
        bytes32 digest = hashOrder(order);
        address signer = ECDSA.recover(digest, signature);
        return signer == order.from;  // 签名者是否与订单发起者一致
    }
}
```

在这个例子中，我们定义了一个 `Order` 结构体和一些用于生成和验证签名的方法。`hashOrder` 方法用于生成符合 EIP-712 标准的数据哈希，而 `verifyOrder` 方法则用于验证签名是否真实来自预期的签名者。


## 总结

通过 EIP-712, Solidity 开发者可以实施更安全、更清晰的用户数据签名方案。这不仅提升了合约的安全性，也优化了用户体验。尽管实施上有一定的门槛，但其带来的好处是非常显著的。

希望本章内容能帮助你理解和运用 EIP-712 来加强你的区块链应用安全性和用户体验。